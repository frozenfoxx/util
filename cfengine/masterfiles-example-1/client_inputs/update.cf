########################
# Client - update.cf
########################

## Author:	FOXX (frozenfoxx@github.com)
## Date:	12/30/2010
## Description:	this example is the central control
##		over updating CFengine's own configuration.

#########################################################
bundle agent update
{
vars:
        "master_location" string =>     "/var/cfengine/masterfiles/client_inputs";
        "master_modules" string =>      "/var/cfengine/masterfiles/client_modules";
        "phost" string =>               "cfmps.somedomain.net";

files:

# Ensure directory permissions
        "/var/cfengine/"
                handle =>       "base_directory",
                comment =>      "Ensure proper permissions on cfengine",
                perms =>        um_u_g("0700","root","root"),
                depth_search => avoid_inputs_urecurse("inf"),
                action =>       uimmediate;

# Update the config files from the MPS
        "/var/cfengine/inputs" 
                handle =>       "update_policy_inputs",
                comment =>      "Update the input files from the policy server",
                perms =>        um_u_g("0600","root","root"),
                copy_from =>    uremote_copy("$(master_location)","${phost}"),
                depth_search => urecurse("inf"),
                action =>       uimmediate;

# Update the module files from the MPS
        "/var/cfengine/modules" 
                handle =>       "update_policy_modules",
                comment =>      "Update the module files from the policy server",
                perms =>        um_u_g("0600","root","root"),
                copy_from =>    uremote_copy("$(master_modules)","${phost}"),
                depth_search => urecurse("inf"),
                action =>       uimmediate;

# Update the binaries from the sbin directory
        "/var/cfengine/bin" 
                handle =>       "update_binaries",
                comment =>      "Update the binary files from the policy server",
                perms => um_u_g("0700","root","root"),
                copy_from => umycopy("/usr/local/sbin","localhost"),
                depth_search => urecurse("inf"),
                action => uimmediate;
}

#########################################################
body perms um_u_g(m,u,g)
{
        mode =>         "$(m)";
        owners =>       { "$(u)" };
        groups =>       { "$(g)" };
}

#########################################################
body depth_search avoid_inputs_urecurse(d)
{
        depth =>        "$(d)";
        exclude_dirs => {       "/var/cfengine/inputs", 
                                "/var/cfengine/state",
                                "/var/cfengine/modules" };
}

############################################
body depth_search urecurse(d)
{
        depth =>         "$(d)";
}

#########################################################
body copy_from uremote_copy(sourcedir,sourceserver)
{
        source =>       "$(sourcedir)";
        servers =>      { "$(sourceserver)" };
        copy_backup =>  "true";
        purge =>        "true";
        trustkey =>     "true";
        compare =>      "digest";
        encrypt =>      "true";
        verify =>       "true";
}

#########################################################
body copy_from umycopy(from,server)
{
        source =>       "$(from)";
        servers =>      { "$(server)" };
        compare =>      "digest";
        purge =>        "true";
}

#########################################################
body action uimmediate
{
        ifelapsed =>    "1";
}


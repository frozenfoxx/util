#######################
# Client - ntp.cf 
#######################

## Name:	ntp.cf
## Author:	FOXX (frozenfoxx@github.com)
## Date:	12/30/2010
## Notes:	Depending on the input file you may end up with effectively duplicate
##		but grammatically different files.  After running once check the ntp.conf,
##		clean up if necessary, then it will keep it as promised.  There's a
##		multitude of ways to fix this if that's too much work.


bundle agent ntp
{
        vars:
          "ntp_server_list" slist =>    {
            "server1", "server2", "server3"
          };

          "driftfile" string => "/etc/ntp.drift";

          "ntp1" slist => {
            "server 0.north-america.pool.ntp.org",
            "server 1.north-america.pool.ntp.org",
            "server 2.north-america.pool.ntp.org",
            "server 3.north-america.pool.ntp.org",
            "driftfile /etc/ntp.drift"
          };

          "ntp2" slist => {
            "server timekeeper",
            "driftfile /etc/ntp.drift"
          };

        classes:
          "ntp_servers" or => { classmatch(canonify("$(ntp_server_list)")) };

        files:
          ntp_servers::
            "/etc/inet/ntp.conf"
            comment =>          "Set up ntp.conf for root NTP servers",
            create =>           "true",
            perms =>            m_u_g("0644","root","sys"),
            edit_line =>        AppendIfNoLine( "${ntp1}" );

          !ntp_servers::
            "/etc/inet/ntp.conf"
            comment =>          "Set up ntp.conf for non-root NTP systems",
            create =>           "true",
            perms =>            m_u_g("0644","root","sys"),
            edit_line =>        AppendIfNoLine( "${ntp2}" );
}

bundle edit_line AppendIfNoLine(input)
{
        insert_lines:
          "$(input)" location => "append";
}

body location append
{
        before_after => "after";
}

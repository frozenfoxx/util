########################
# Server - update.cf
########################

## Author:	FOXX (frozenfoxx@github.com)
## Date:	12/29/2010
## Description:	this example is the central control
##		over updating CFengine's own configuration.

#########################################################
bundle agent update
{
vars:
        "master_location" string => "/var/cfengine/masterfiles";

# Define Master Policy Servers
        "phost" string =>       "localhost";

files:

# Ensure directory permissions
        "/var/cfengine/"
                perms =>        um_u_g("0700","root","root"),
                depth_search => avoid_inputs_urecurse("inf"),
                action =>       uimmediate;

# Update the config files from the MPS
        "/var/cfengine/inputs" 
                perms =>        um_u_g("0600","root","root"),
                copy_from =>    umycopy("$(master_location)","localhost"),
                depth_search => urecurse("inf"),
                action =>       uimmediate;

# Update the binaries from the sbin directory
        "/var/cfengine/bin" 
                perms => um_u_g("0700","root","root"),
                copy_from => umycopy("/usr/local/sbin","localhost"),
                depth_search => urecurse("inf"),
                action => uimmediate;
}

#########################################################
body perms um_u_g(m,u,g)
{
        mode =>         "$(m)";
        owners =>       { "$(u)" };
        groups =>       { "$(g)" };
}

#########################################################
body depth_search avoid_inputs_urecurse(d)
{
        depth =>        "$(d)";
        exclude_dirs => { "/var/cfengine/inputs", "/var/cfengine/state" };
}

############################################
body depth_search urecurse(d)
{
        depth =>         "$(d)";
}

#########################################################
body copy_from umycopy(from,server)
{
        source =>       "$(from)";
	servers =>	{ "${server}" };
        compare =>      "digest";
        purge =>        "true";
}

#########################################################
body action uimmediate
{
        ifelapsed =>    "1";
}
